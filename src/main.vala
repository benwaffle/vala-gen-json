class Field : Object, Json.Serializable {
    public string name { get; set; }
    public string typename;
    public bool required { get; set; default = true; }
    public string? description { get; set; }

    ParamSpec tps = new ParamSpecString ("type", "type", "blurb", null, ParamFlags.READWRITE);

    public override void set_property (ParamSpec pspec, Value value) {
        if (pspec.get_name () == "type") {
            typename = (string) value;
        } else {
            base.set_property (pspec.get_name (), value);
        }
    }

    public override unowned ParamSpec? find_property (string name) {
        if (name == "type") {
            return tps;
        }
        return this.get_class ().find_property (name);
    }
}

class Model : Object, Json.Serializable {
    public string? description { get; set; }
    public GenericArray<Field> fields { get; set; default = new GenericArray<Field>(); }

    public override bool deserialize_property (string prop_name, out Value val, ParamSpec pspec, Json.Node property_node) {
        if (prop_name == "description") {
            return default_deserialize_property (prop_name, out val, pspec, property_node);
        } else if (prop_name == "fields") {
            var fields = new GenericArray<Field> ();
            property_node.get_array ().foreach_element ((arr, idx, node) => {
                var field = Json.gobject_deserialize (typeof (Field), node) as Field;
                assert (field != null);
                fields.add (field);
            });
            val = fields;
            return true;
        } else {
            warning (@"unknown field $prop_name\n");
            return false;
        }
    }
}

string modelToClassName(string modelName) {
    return /(?:^|_)(.)/.replace (modelName, -1, 0, "\\U\\1");
}

int main(string[] args) {
    var parser = new Json.Parser ();
    // TODO arg parsing
    try {
        parser.load_from_file (args[1]);
    } catch (Error e) {
        print (e.message);
        return 1;
    }

    var output = FileStream.open (args[2], "w");

    output.printf ("""/*
Autogenerated by vala-gen-json 0.0.1
https://github.com/benwaffle/vala-gen-json)
*/""");

    output.printf ("\nnamespace Apibuilder {\n");

    Json.Object? models = parser.get_root ().get_object ().get_object_member ("models");
    models.foreach_member ((obj, member, node) => {
        var model = Json.gobject_deserialize (typeof (Model), node) as Model;

        output.printf(@"class $(modelToClassName(member)) {\n");
        //  print (@"Model: $member - $(model.description ?? "")\n");
        model.fields.foreach ((field) => {
            output.printf(@"public $(field.typename) $(field.name)$(field.required ? "" : "?") { get; set; }\n");
            //  print (@"\t$(field.name): $(field.typename) $(field.required ? "" : "?") - $(field.description ?? "")\n");
        });
        output.printf("}\n");
    });

    return 0;
}